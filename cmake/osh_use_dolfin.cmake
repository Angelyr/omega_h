#trying to deal with the lack of standardized CMake support for exporting
#dependencies on upstream packages, which DOLFIN suffers from like most
#other packages, except Omega_h which benefits from bob.cmake

macro(osh_use_dolfin)
  bob_public_dep(DOLFIN)
  if (Omega_h_USE_DOLFIN)
    get_target_property(DOLFIN_INTERFACE_LIBS dolfin INTERFACE_LINK_LIBRARIES)
    foreach(dep_lib IN LISTS DOLFIN_INTERFACE_LIBS)
      if ("${dep_lib}" STREQUAL Boost::timer)
        set(DOLFIN_BOOST_COMPONENTS ${DOLFIN_BOOST_COMPONENTS} timer)
      elseif ("${dep_lib}" STREQUAL Boost::filesystem)
        set(DOLFIN_BOOST_COMPONENTS ${DOLFIN_BOOST_COMPONENTS} filesystem)
      elseif ("${dep_lib}" STREQUAL PETSc::petsc)
        set(DOLFIN_USES_PETSc True)
      elseif ("${dep_lib}" STREQUAL SLEPC::slepc)
        set(DOLFIN_USES_SLEPC True)
      endif()
    endforeach()
    if (DOLFIN_BOOST_COMPONENTS)
      set(Omega_h_USE_Boost_DEFAULT ON)
      bob_add_dependency(PUBLIC NAME Boost COMPONENTS timer)
    endif()
    if (DOLFIN_USES_PETSc)
      set(Omega_h_USE_PETSc_DEFAULT ON)
      bob_add_dependency(PUBLIC NAME PETSc)
    endif()
    if (DOLFIN_USES_SLEPC)
      set(Omega_h_USE_SLEPC ON)
      bob_add_dependency(PUBLIC NAME SLEPC)
    endif()
  endif()
endmacro(osh_use_dolfin)
