set(Omega_h_VERSION @OSH_VERSION@)

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
# we will use find_dependency, but we don't want to force
# our users to have to specify where all of our dependencies
# were installed; that defeats the whole point of automatically
# importing dependencies.
# since the documentation for find_dependency() doesn't mention
# a PATHS argument, we'll temporarily add the prefixes to
# CMAKE_PREFIX_PATH.
set(Omega_h_DEPS "@OSH_DEPS@")
set(Omega_h_DEP_PREFIXES "@OSH_DEP_PREFIXES@")
set(Omega_h_BACKUP_PREFIX_PATH "${CMAKE_PREFIX_PATH}")
set(CMAKE_PREFIX_PATH "${Omega_h_DEP_PREFIXES};${CMAKE_PREFIX_PATH}")
foreach(dep IN LISTS Omega_h_DEPS)
  find_dependency(${dep})
endforeach()
set(CMAKE_PREFIX_PATH "${Omega_h_BACKUP_PREFIX_PATH}")

set(OSH_TARGETS "@OSH_TARGETS@")
foreach(target IN LISTS OSH_TARGETS)
  include(${CMAKE_CURRENT_LIST_DIR}/${target}-targets.cmake)
endforeach()

set_and_check(Omega_h_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")
set_and_check(Omega_h_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set(Omega_h_INCLUDE_DIRS ${Omega_h_INCLUDE_DIR} @DEP_INCLUDE_DIRS@)
set(Omega_h_COMPILER "@CMAKE_CXX_COMPILER@")
set(Omega_h_FLAGS "@FLAGS@")
