include(CMakeFindDependencyMacro)
# we will use find_dependency, but we don't want to force
# our users to have to specify where all of our dependencies
# were installed; that defeats the whole point of automatically
# importing dependencies.
# since the documentation for find_dependency() doesn't mention
# a PATHS argument, we'll temporarily add the prefixes to
# CMAKE_PREFIX_PATH.
set(Omega_h_PREFIX_PATH "${CMAKE_PREFIX_PATH}")
if(@OSH_USE_KOKKOS@)
  set(CMAKE_PREFIX_PATH "@Kokkos_PREFIX@;${CMAKE_PREFIX_PATH}")
  find_dependency(Kokkos)
  set(CMAKE_PREFIX_PATH "${Omega_h_PREFIX_PATH}")
endif()
set(OSH_TARGETS "@OSH_TARGETS@")
foreach(target IN LISTS OSH_TARGETS)
  include(${CMAKE_CURRENT_LIST_DIR}/${target}-targets.cmake)
endforeach()
get_filename_component(Omega_h_LIB_DIR "${CMAKE_CURRENT_LIST_DIR}" PATH)
get_filename_component(Omega_h_INCLUDE_DIR "${Omega_h_LIB_DIR}/../include" ABSOLUTE)
set(Omega_h_INCLUDE_DIRS ${Omega_h_INCLUDE_DIR} @DEP_INCLUDE_DIRS@)
set(Omega_h_COMPILER "@CMAKE_CXX_COMPILER@")
set(Omega_h_FLAGS "@FLAGS@")
